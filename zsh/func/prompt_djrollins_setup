# prompt setup derived from steeefs theme:
# https://github.com/robbyrussell/oh-my-zsh/blob/master/themes/steeef.zsh-theme
# Modified to update git on every command when inside git repo
#
# Original comments:
#
# prompt style and colors based on Steve Losh's Prose theme:
# http://github.com/sjl/oh-my-zsh/blob/master/themes/prose.zsh-theme
#
# vcs_info modifications from Bart Trojanowski's zsh prompt:
# http://www.jukie.net/bart/blog/pimping-out-zsh-prompt
#
# git untracked files modification from Brian Carper:
# http://briancarper.net/blog/570/git-info-in-your-zsh-prompt

export VIRTUAL_ENV_DISABLE_PROMPT=1

function virtualenv_info {
	[ $VIRTUAL_ENV ] && echo '('%F{blue}`basename $VIRTUAL_ENV`%f') '
}

# First time in, update git section of prompt
PR_GIT_UPDATE=1

setopt prompt_subst

autoload -U add-zsh-hook
autoload -Uz vcs_info

#use extended color pallete if available
turquoise="%F{cyan}"
orange="%F{yellow}"
purple="%F{magenta}"
hotpink="%F{red}"
limegreen="%F{green}"

# enable VCS systems you use
zstyle ':vcs_info:*' enable git svn

# check-for-changes can be really slow.
# you should disable it, if you work with large repositories
zstyle ':vcs_info:*:prompt:*' check-for-changes true

# set formats
# %b - branchname
# %u - unstagedstr (see below)
# %c - stagedstr (see below)
# %a - action (e.g. rebase-i)
# %R - repository path
# %S - path in the repository
PR_RST="%f"
FMT_BRANCH="(%{$turquoise%}%b%u%c${PR_RST})"
FMT_ACTION="(%{$limegreen%}%a${PR_RST})"
FMT_UNSTAGED="%{$orange%}!"
FMT_STAGED="%{$limegreen%}!"

zstyle ':vcs_info:*:prompt:*' unstagedstr	"${FMT_UNSTAGED}"
zstyle ':vcs_info:*:prompt:*' stagedstr	"${FMT_STAGED}"
zstyle ':vcs_info:*:prompt:*' actionformats "${FMT_BRANCH}${FMT_ACTION}"
zstyle ':vcs_info:*:prompt:*' formats	"${FMT_BRANCH}"
zstyle ':vcs_info:*:prompt:*' nvcsformats	""

function steeef_chpwd {
	PR_GIT_UPDATE=1
}
add-zsh-hook chpwd steeef_chpwd

function steeef_precmd {
	if [[ -n "$PR_GIT_UPDATE" ]]; then
	# check for untracked files or updated submodules, since vcs_info doesn't
	if git ls-files --other --exclude-standard 2> /dev/null | grep -q "."; then
		FMT_BRANCH="(%{$turquoise%}%b%{$hotpink%}!%u%c${PR_RST})"
	else
		FMT_BRANCH="(%{$turquoise%}%b%u%c${PR_RST})"
	fi

	zstyle ':vcs_info:*:prompt:*' formats "${FMT_BRANCH} "

	vcs_info 'prompt'

	# check if we're up to 2 directories deep into a git repository
	if [[ -d ".git" || -d "../.git" || -d "../../.git" ]]; then
		PR_GIT_UPDATE=1
	else
		PR_GIT_UPDATE=
	fi
	fi
}
add-zsh-hook precmd steeef_precmd

PROMPT=$'%{$purple%}%n${PR_RST} at %{$orange%}%m${PR_RST} in %{$limegreen%}%~${PR_RST} $vcs_info_msg_0_$(virtualenv_info)
$ '
